project(SISPS-PV-MCU-SM72445-Test)

set(TEST_EXECUTABLE ${PROJECT_NAME})

file(GLOB APPLICATION_SOURCES
	${CMAKE_CURRENT_SOURCE_DIR}/Src/*.c
	${CMAKE_CURRENT_SOURCE_DIR}/Src/*.cpp
)

add_executable(${TEST_EXECUTABLE}
	${APPLICATION_SOURCES}
	${STARTUP_SCRIPT}
)

target_include_directories(${TEST_EXECUTABLE} PRIVATE
	${CMAKE_CURRENT_SOURCE_DIR}/Inc
)

target_link_libraries(${TEST_EXECUTABLE} PRIVATE
	SISPS::HAL
	SISPS::SM72445
)

target_link_options(${TEST_EXECUTABLE} PRIVATE
	LINKER:-Map=${TEST_EXECUTABLE}.map,--cref
)

option(SM72445_EXCLUDE_FROM_ALL "Exclude ${TEST_EXECUTABLE} from \"build all\"" OFF)
if(SM72445_EXCLUDE_FROM_ALL)
	set_target_properties(${TEST_EXECUTABLE} PROPERTIES
		EXCLUDE_FROM_ALL TRUE
	)
endif()

add_custom_command(TARGET ${TEST_EXECUTABLE} POST_BUILD
	COMMENT "Printing ${TEST_EXECUTABLE} Size..."
	COMMAND ${CMAKE_SIZE} $<TARGET_FILE:${TARGET_EXECUTABLE}>
)

add_custom_command(TARGET ${TEST_EXECUTABLE} POST_BUILD
	COMMENT "Generating ${TEST_EXECUTABLE}.hex and ${TEST_EXECUTABLE}.bin..."
	COMMAND ${CMAKE_OBJCOPY} -O ihex $<TARGET_FILE:${TEST_EXECUTABLE}> ${TEST_EXECUTABLE}.hex
	COMMAND ${CMAKE_OBJCOPY} -O binary $<TARGET_FILE:${TEST_EXECUTABLE}> ${TEST_EXECUTABLE}.bin
)

add_custom_command(TARGET ${TEST_EXECUTABLE} POST_BUILD
	COMMENT "Generating ${TEST_EXECUTABLE} Disassembly..."
	COMMAND ${CMAKE_OBJDUMP} -D -C $<TARGET_FILE:${TARGET_EXECUTABLE}> > ${TARGET_EXECUTABLE}.s
)
